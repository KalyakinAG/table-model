Перем Таблицы Экспорт;
Перем Колонки;
Перем Колонка;

Функция Таблица(Имя) Экспорт
	Колонки = Новый Массив;
	Таблицы.Вставить(Имя, Колонки);
	Колонка = Неопределено;
	Возврат ЭтотОбъект;
КонецФункции

Функция Колонка(Имя) Экспорт
	Колонка = Новый Структура("Имя", Имя);
	Колонки.Добавить(Колонка);
	Возврат ЭтотОбъект;
КонецФункции

Функция ТипСтрока(Длина) Экспорт
	Колонка.Вставить("Тип", ОбщегоНазначения.ОписаниеТипаСтрока(Длина));
	Возврат ЭтотОбъект;
КонецФункции

Функция ТипЧисло(Разрядность, РазрядностьДробнойЧасти = 0, ЗнакЧисла = Неопределено) Экспорт
	Колонка.Вставить("Тип", ОбщегоНазначения.ОписаниеТипаЧисло(Разрядность, РазрядностьДробнойЧасти, ЗнакЧисла));
	Возврат ЭтотОбъект;
КонецФункции

Функция ТипБулево() Экспорт
	Колонка.Вставить("Тип", Новый ОписаниеТипов("Булево"));
	Возврат ЭтотОбъект;	
КонецФункции

Функция ТипПроизвольный() Экспорт
	Колонка.Вставить("Тип", Новый ОписаниеТипов);
	Возврат ЭтотОбъект;	
КонецФункции

Процедура СоздатьТаблицыВРеквизитахФормы(НовыеРеквизиты = Неопределено) Экспорт
	Перем Колонка;
	Перем Тип;
	Если НовыеРеквизиты = Неопределено Тогда
		НовыеРеквизиты = Новый Массив;
	КонецЕсли;
	Для Каждого ЭлементТаблицы Из Таблицы Цикл
		НовыеРеквизиты.Добавить(Новый РеквизитФормы(ЭлементТаблицы.Ключ, Новый ОписаниеТипов("ТаблицаЗначений")));
		Для Каждого Колонка Из ЭлементТаблицы.Значение Цикл
			Если Колонка.Свойство("Тип", Тип) Тогда
				НовыеРеквизиты.Добавить(Новый РеквизитФормы(Колонка.Имя, Тип, ЭлементТаблицы.Ключ));
			Иначе
				НовыеРеквизиты.Добавить(Новый РеквизитФормы(Колонка.Имя, Новый ОписаниеТипов("Неопределено"), ЭлементТаблицы.Ключ));
			КонецЕсли;						
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура СоздатьТаблицыВСтруктуре(Контекст) Экспорт
	Перем Таблица;
	Перем Колонки;
	Перем Колонка;
	Перем Тип;
	Для Каждого ЭлементТаблицы Из Таблицы Цикл
		Таблица = Новый ТаблицаЗначений;
		Колонки = Таблица.Колонки;
		Для Каждого Колонка Из ЭлементТаблицы.Значение Цикл
			Если Колонка.Свойство("Тип", Тип) Тогда
				Колонки.Добавить(Колонка.Имя, Тип);						
			Иначе
				Колонки.Добавить(Колонка.Имя, Новый ОписаниеТипов("Неопределено"));
			КонецЕсли;
		КонецЦикла;
		Контекст.Вставить(ЭлементТаблицы.Ключ, Таблица);
	КонецЦикла;
КонецПроцедуры

Таблицы = Новый Структура;